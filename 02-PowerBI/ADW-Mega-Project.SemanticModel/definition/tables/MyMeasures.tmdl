table MyMeasures
	lineageTag: 872739a5-9f66-454a-a5de-20fd3efe1846

	measure 'Total Tax' = SUM(FactSales[TotalTax-Fixed])
		formatString: \$#,0.0;(\$#,0.0);\$#,0.0
		displayFolder: 01.Revenue metrics
		lineageTag: b4fa57ef-3c46-4964-be40-f6eedb2a9355

	measure 'Total Freight' = SUM(FactSales[TotalFreight-Fixed])
		formatString: \$#,0.0;(\$#,0.0);\$#,0.0
		displayFolder: 01.Revenue metrics
		lineageTag: 21589bb7-3d3a-4946-9d29-d588456d7a31

	measure 'Subtotal Amount' = SUM(FactSales[LineTotal])
		formatString: \$#,0.00;(\$#,0.00);\$#,0.00
		displayFolder: 01.Revenue metrics
		lineageTag: cfe4bab4-6eef-4f6e-b010-2638c690a229

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	measure 'Total Revenue' = [Subtotal Amount] + [Total Freight] + [Total Tax]
		formatString: \$#,0.00;(\$#,0.00);\$#,0.00
		displayFolder: 01.Revenue metrics
		lineageTag: 157f5daf-cebe-4653-a50a-f7337d11abf7

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	measure 'Revenue Variance' = [Total Revenue (Order-level)] - [Total Revenue]
		displayFolder: 01.Revenue metrics
		lineageTag: 841edf98-fcd8-4b45-a060-1ba5a30e0af1

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Total Orders' = DISTINCTCOUNT(FactSales[SalesOrderID])
		formatString: #,0.000
		displayFolder: 02.Order & Quantity
		lineageTag: bcefa950-aeb3-49e4-a2f5-eb488397c72d

	measure 'Total Quantity' = SUM(FactSales[OrderQty])
		formatString: #,0.00
		displayFolder: 02.Order & Quantity
		lineageTag: 3c3a89fa-bf32-413f-bab7-2258e067d10f

	measure AOV = DIVIDE([Total Revenue], [Total Orders], 0)
		formatString: \$#,0.0;(\$#,0.0);\$#,0.0
		displayFolder: 02.Order & Quantity
		lineageTag: 1b63b318-4335-4c33-af09-46688fb9d464

	measure 'Average Line Value' = DIVIDE([Subtotal Amount], COUNTROWS(FactSales), 0)
		formatString: \$#,0.0;(\$#,0.0);\$#,0.0
		displayFolder: 02.Order & Quantity
		lineageTag: 3b8668a6-2995-4bb7-a96e-a103ba4851b6

	measure 'Lines per Order' = DIVIDE(COUNTROWS(FactSales), [Total Orders], 0)
		displayFolder: 02.Order & Quantity
		lineageTag: 845e3fd9-9cb5-4c02-820c-a55a3ca33583

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Average Quantity per Order' = DIVIDE([Total Quantity], [Total Orders], 0)
		displayFolder: 02.Order & Quantity
		lineageTag: b06b61ca-f096-4e25-8221-ca32955de13f

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Total COGS' = SUM(FactSales[COGS])
		formatString: \$#,0.00;(\$#,0.00);\$#,0.00
		displayFolder: 03.Cost & Profit
		lineageTag: 82dbbdfd-cee7-40db-b3a2-61fc32c7b575

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	measure 'Gross Profit' = SUM(FactSales[GrossProfit])
		formatString: \$#,0.00;(\$#,0.00);\$#,0.00
		displayFolder: 03.Cost & Profit
		lineageTag: 6f0a6eb1-bacc-4cd6-a47d-fc24f8f7869a

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	measure 'Gross Margin %' = DIVIDE([Gross Profit], [Total Revenue], 0)
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: 03.Cost & Profit
		lineageTag: aeda87f1-4486-4a62-9655-04cb921800e3

	measure 'Average Unit Cost' = DIVIDE([Total COGS], [Total Quantity], 0)
		formatString: \$#,0.0;(\$#,0.0);\$#,0.0
		displayFolder: 03.Cost & Profit
		lineageTag: 317c9a03-45d8-4740-bd84-0c498c1d2472

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	measure 'Average Unit Price' = DIVIDE([Subtotal Amount], [Total Quantity], 0)
		formatString: \$#,0.00;(\$#,0.00);\$#,0.00
		displayFolder: 03.Cost & Profit
		lineageTag: 057ecdf2-e5d1-4170-a30c-331adfdb0358

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	measure 'Markup %' = DIVIDE([Average Unit Price] - [Average Unit Cost], [Average Unit Cost], 0)
		displayFolder: 03.Cost & Profit
		lineageTag: ba15b60e-151a-4243-b62d-55ca2d7f13d1

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Revenue LY' = CALCULATE([Total Revenue], SAMEPERIODLASTYEAR(DimDate[Date]))
		displayFolder: 04.Time Intelligence
		lineageTag: c1ebcc8a-68f8-400a-a8ba-88dcb3e83bd2

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Revenue YoY %' = DIVIDE([Total Revenue] - [Revenue LY], [Revenue LY], 0)
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: 04.Time Intelligence
		lineageTag: 7c83bb3d-f359-405e-9640-5b77594cce7f

	measure 'Revenue PM' = CALCULATE([Total Revenue], DATEADD(DimDate[Date], -1, MONTH))
		displayFolder: 04.Time Intelligence
		lineageTag: 3e38336c-cb30-4d7a-8ed3-a5ba8494b2be

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'MoM %' = DIVIDE([Total Revenue] - [Revenue PM], [Revenue PM], 0)
		displayFolder: 04.Time Intelligence
		lineageTag: d34b7592-a0cc-41dd-a9a8-0221a7fa886b

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Revenue PQ' = CALCULATE([Total Revenue], DATEADD(DimDate[Date], -1, QUARTER))
		displayFolder: 04.Time Intelligence
		lineageTag: 92b5153c-682e-4106-9802-e928af08a0cd

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Revenue QoQ %' = DIVIDE([Total Revenue] - [Revenue PQ], [Revenue PQ], 0)
		displayFolder: 04.Time Intelligence
		lineageTag: b522517d-5b4c-4e8e-ad10-c8d78262f254

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Revenue YTD' = TOTALYTD([Total Revenue], DimDate[Date])
		formatString: \$#,0.0;(\$#,0.0);\$#,0.0
		displayFolder: 04.Time Intelligence
		lineageTag: 1423cfcc-5837-4b59-9904-68ae89b55b56

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	measure 'Orders YTD' = CALCULATE([Total Orders], DATESYTD(DimDate[Date]))
		formatString: 0
		displayFolder: 04.Time Intelligence
		lineageTag: 531bd07d-2e84-4863-a82c-07fe9d376ed8

	measure 'Revenue PYTD' = CALCULATE([Revenue YTD], SAMEPERIODLASTYEAR(DimDate[Date]))
		displayFolder: 04.Time Intelligence
		lineageTag: 3306c714-ee9e-44b1-b85e-56a97c84d1e0

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'YTD vs PYTD %' = DIVIDE([Revenue YTD] - [Revenue PYTD], [Revenue PYTD], 0)
		displayFolder: 04.Time Intelligence
		lineageTag: 5a5e74b0-0509-4a51-9987-01d88033a733

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Revenue 3M Avg' = ```
			
			        CALCULATE([Total Revenue],
			            DATESINPERIOD(DimDate[Date], LASTDATE(DimDate[Date]), -3, MONTH)
			        ) / 3
			```
		displayFolder: 04.Time Intelligence
		lineageTag: 1645cd94-7e3d-4e95-a67c-51553f22fd49

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Revenue 12M Avg' = ```
			
			    CALCULATE(
			    [Total Revenue],
			    DATESINPERIOD(DimDate[Date], LASTDATE(DimDate[Date]), -12, MONTH)
			    ) / 12
			```
		displayFolder: 04.Time Intelligence
		lineageTag: 0cf40ed5-06fb-4d9c-af15-c16cebbfe12e

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Revenue R12M' = ```
			
			    CALCULATE(
			    [Total Revenue],
			    DATESINPERIOD(DimDate[Date], LASTDATE(DimDate[Date]), -12, MONTH)
			    )
			```
		displayFolder: 04.Time Intelligence
		lineageTag: c3bbda11-97b4-4073-8c2c-86323e7362b5

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Total Customers (All)' = COUNTROWS(DimCustomer)
		formatString: #,0.0
		displayFolder: 05.Customer metrics
		lineageTag: 52058cf5-259e-4ee3-b940-603a7688444f

	measure 'Active Customers' = DISTINCTCOUNT(FactSales[CustomerID])
		formatString: #,0.0
		displayFolder: 05.Customer metrics
		lineageTag: dd51f329-5980-42c6-a5d5-bdff33975b21

	measure 'Customer Activation Rate' = DIVIDE([Active Customers], [Total Customers (All)], 0)
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: 05.Customer metrics
		lineageTag: b0bd207c-c0b7-4392-af4d-0ab2cea4cda1

	measure 'New Customers' = ```
			
			        VAR CurrentCustomers = VALUES(FactSales[CustomerID])
			        VAR PreviousCustomers =
			            CALCULATETABLE(
			                VALUES(FactSales[CustomerID]),
			                DATEADD(DimDate[Date], -1, YEAR)
			            )
			        RETURN COUNTROWS(EXCEPT(CurrentCustomers, PreviousCustomers))
			```
		formatString: #,0.0
		displayFolder: 05.Customer metrics
		lineageTag: 52f728c1-851f-4220-af42-6e446056adbf

	measure 'Returning Customers' = [Active Customers] - [New Customers]
		formatString: #,0.0
		displayFolder: 05.Customer metrics
		lineageTag: 33758a22-ca73-4833-828f-7438320cff42

	measure 'Customer Lifetime Value' = ```
			
			        AVERAGEX(VALUES(DimCustomer[CustomerID]), CALCULATE([Total Revenue]))
			```
		displayFolder: 05.Customer metrics
		lineageTag: 3eb3df66-229e-41dc-8e45-6f255e40984d

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Avg Orders per Customer' = DIVIDE([Total Orders], [Active Customers], 0)
		displayFolder: 05.Customer metrics
		lineageTag: 72c93b3f-440a-493b-b69e-aa29a8cb34fb

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Customer Retention Rate' = ```
			
			    VAR PrevCust =
			        CALCULATETABLE(
			            VALUES(FactSales[CustomerID]),
			            PARALLELPERIOD(DimDate[Date], -1, YEAR)
			        )
			    VAR CurrCust = VALUES(FactSales[CustomerID])
			    VAR RetainedCust = COUNTROWS(INTERSECT(CurrCust, PrevCust))
			    VAR PrevCustCount = COUNTROWS(PrevCust)
			    RETURN
			    IF(
			         PrevCustCount = 0 || ISBLANK(PrevCustCount),
			        0,
			        DIVIDE(RetainedCust, PrevCustCount)
			    )
			
			```
		displayFolder: 05.Customer metrics
		lineageTag: a194407a-9d0e-4eba-ac03-8a8f31f8c2aa

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Total Products (All)' = COUNTROWS(DimProduct)
		formatString: 0
		displayFolder: 06.Product metrics
		lineageTag: a5e86dab-d956-48f1-bba6-7476ec078262

	measure 'Products Sold' = DISTINCTCOUNT(FactSales[ProductID])
		formatString: 0
		displayFolder: 06.Product metrics
		lineageTag: 2595be6c-5a8c-4284-a9ec-3edcab496299

	measure 'Product Revenue Rank' = ```
			
			    RANKX(
			     FILTER(ALL(DimProduct), NOT ISBLANK([Total Revenue])),
			    [Total Revenue],
			    ,
			    DESC,
			    DENSE
			    )
			```
		formatString: 0
		displayFolder: 06.Product metrics
		lineageTag: e13ad407-444e-4e19-ae32-da45387e32be

	measure 'Is Top 10 Product' = IF([Product Revenue Rank] <= 10, "Yes", "No")
		displayFolder: 06.Product metrics
		lineageTag: af23aa91-4f6e-470a-98ca-a08b045abaf7

	measure 'Product Revenue %' = ```
			
			    DIVIDE(
			    [Total Revenue],
			    CALCULATE([Total Revenue], ALL(DimProduct)),
			    0
			    )
			```
		formatString: 0.000%;-0.000%;0.000%
		displayFolder: 06.Product metrics
		lineageTag: 5916d7d4-5404-44e8-b343-9dcb8e049071

	measure 'Territory Revenue Rank' = ```
			
				RANKX(
				ALL(DimTerritory[TerritoryName]),
				[Total Revenue],
				,
				DESC,
				DENSE
				)
			```
		formatString: 0
		displayFolder: 07.Territory metrics
		lineageTag: e23b2e1c-74d7-47d0-b182-3ee5b9e114aa

	measure 'Territory Revenue %' = ```
			
				DIVIDE(
				[Total Revenue],
				CALCULATE([Total Revenue], ALL(DimTerritory)),
				0
				)
			```
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: 07.Territory metrics
		lineageTag: 53198812-4739-4295-92d4-ed6e458ed684

	measure 'Total Purchases' = ```
			
			           COALESCE( //keep the BLANKs and simply format them as 0: vendors doesn't purchse
			            SUMX(
			            VALUES(FactPurchasing[PurchaseOrderID]),
			            CALCULATE(MAX(FactPurchasing[TotalDue]))
			        ), 0
			        )
			```
		formatString: \$#,0.00;(\$#,0.00);\$#,0.00
		displayFolder: 09.Purchasing metrics
		lineageTag: 2e6faced-b525-4ec6-a3c1-82b0bb821257

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	measure 'Total Purchase Orders' = ```
			
			            COALESCE(
			                DISTINCTCOUNT(FactPurchasing[PurchaseOrderID]),
			                0
			            )
			```
		formatString: #,0
		displayFolder: 09.Purchasing metrics
		lineageTag: 593903d6-1303-43a7-aa89-1a3e24525036

	measure 'Avg Purchase Order Value' = ```
			
			            COALESCE(
			                DIVIDE([Total Purchases], [Total Purchase Orders], 0),
			                0
			        )
			```
		formatString: \$#,0.00;(\$#,0.00);\$#,0.00
		displayFolder: 09.Purchasing metrics
		lineageTag: b52e0b0c-cb6e-48c2-a582-bc352455d791

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	measure 'Total Purchase Quantity' = ```
			
			          COALESCE(
			            SUM(FactPurchasing[OrderQty]),
			            0
			        )
			```
		formatString: #,0.00
		displayFolder: 09.Purchasing metrics
		lineageTag: 62b1659d-8dff-4097-8477-31b391bd20f2

	measure 'Total Units Sold' = ```
			
			        SUM(FactSales[OrderQty])
			```
		formatString: 0
		displayFolder: 10.Supply & Demand
		lineageTag: 577db2e8-925e-4555-875e-c4fdbd8d200a

	measure 'Supply Demand Gap' = ```
			
			        [Total Stock] - [Total Units Sold]
			```
		formatString: 0
		displayFolder: 10.Supply & Demand
		lineageTag: 2c73b1e7-43ba-46d9-85d5-669f7bfed793

	measure 'Supply Demand Status' = ```
			
			        SWITCH(
			            TRUE(),
			            [Supply Demand Gap] > 1000, "Surplus",
			            [Supply Demand Gap] > 0, "Balanced",
			            [Supply Demand Gap] > -500, "Shortage",
			            "Critical Shortage"
			        )
			```
		displayFolder: 10.Supply & Demand
		lineageTag: da300302-8f91-4fc6-9b3d-83634ab8c40e

	measure 'Total Employees' = ```
			
			        CALCULATE(
			            DISTINCTCOUNT(FactEmployeeHistory[BusinessEntityID]),
			            FactEmployeeHistory[IsCurrentEmployee] = 1
			        )
			```
		formatString: 0
		displayFolder: 11.Hr metrics
		lineageTag: 326600a2-ae27-42e5-8a4a-d80fbe4f0306

	measure 'Male Employees' = ```
			
			           CALCULATE(
			            IF([Total Employees]== BLANK(),
			                0,
			                [Total Employees] 
			            ),
			            DimEmployee[Gender] = "Male"
			        )
			```
		formatString: 0
		displayFolder: 11.Hr metrics
		lineageTag: d026422f-5c27-423c-9430-cc0a62080903

	measure 'Female Employees' = ```
			
			           CALCULATE(
			            IF([Total Employees]== BLANK(),
			                0,
			                [Total Employees] 
			            ),
			            DimEmployee[Gender] = "Female"
			        )
			```
		formatString: 0
		displayFolder: 11.Hr metrics
		lineageTag: 9899ab8d-3545-47ee-8fa8-7fd66da0e139

	measure 'Gender Ratio' = ```
			
			        DIVIDE([Male Employees], [Female Employees], 0)
			```
		displayFolder: 11.Hr metrics
		lineageTag: 58ae5313-e991-4cc3-b520-f0ab3e38a837

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Avg Tenure Years' = ```
			
			        VAR MaxDate = MAX(DimDate[Date])
			        VAR TenureDays =
			            CALCULATE(
			                AVERAGEX(
			                    FILTER(
			                        FactEmployeeHistory,
			                        FactEmployeeHistory[IsCurrentEmployee] = 1
			                    ),
			                    DATEDIFF(FactEmployeeHistory[StartDate], MaxDate, DAY)
			                )
			            )
			        RETURN
			            TenureDays / 365.25
			```
		formatString: #,0.00
		displayFolder: 11.Hr metrics
		lineageTag: 831f1e42-3cb7-4c5f-ba87-da0bf8d657f2

	measure 'Avg Employee Age' = ```
			
			        VAR MaxDate = MAX(DimDate[Date])
			        RETURN
			        AVERAGEX(
			            SELECTCOLUMNS(
			                VALUES(FactEmployeeHistory[BusinessEntityID]), 
			                "BirthDate", LOOKUPVALUE(
			                                DimEmployee[BirthDate],
			                                DimEmployee[BusinessEntityID], 
			                                FactEmployeeHistory[BusinessEntityID]
			                            )
			            ),
			            DATEDIFF([BirthDate], MaxDate, DAY) / 365.25
			)
			```
		formatString: #,0.00
		displayFolder: 11.Hr metrics
		lineageTag: 3b0f715a-cac4-44a2-8417-f3af2da46c79

	measure 'Total Revenue (order-level)' = ```
			
			    SUMX(
			        SUMMARIZECOLUMNS(
			        FactSales[SalesOrderID],
			        "Total Revenue (order-level)",
			        MAX(FactSales[TotalDue])
			        ),
			        [Total Revenue (order-level)]
			    )
			```
		formatString: \$#,0.0;(\$#,0.0);\$#,0.0
		displayFolder: 01.Revenue metrics
		lineageTag: 2197dc97-31a3-4d71-b50d-807ac6f6bfd4

	measure 'Turnover Rate %' = ```
			
			    VAR CurrentDate =
			        MAX(DimDate[Date])  -- âœ… scalar date for context
			    VAR OneYearAgo =
			        DATE(YEAR(CurrentDate) - 1, MONTH(CurrentDate), DAY(CurrentDate))
			    VAR EmployeesLeft =
			        CALCULATE(
			            DISTINCTCOUNT(FactEmployeeHistory[BusinessEntityID]),
			            FactEmployeeHistory[EndDate] <> BLANK(),
			            FactEmployeeHistory[EndDate] >= OneYearAgo
			        )
			    VAR AvgHeadcount = [Total Employees]
			    RETURN
			        DIVIDE(EmployeesLeft, AvgHeadcount, 0)
			```
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: 11.Hr metrics
		lineageTag: 73c6d420-130c-4342-b6fc-dbb462b94f44

	measure 'Total Inventory Value' = ```
			
			        SUM(FactInventory[InventoryValue])
			```
		formatString: \$#,0.00;(\$#,0.00);\$#,0.00
		displayFolder: 08.Inventory metrics
		lineageTag: d1bdcb2c-9be2-4aff-a60d-d466a4c976f1

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	measure 'Total Products in Inventory' = ```
			
			        DISTINCTCOUNT(DimProduct[ProductID])
			```
		formatString: 0
		displayFolder: 08.Inventory metrics
		lineageTag: 46944fa0-251b-4f3d-aaac-2c30f15ed775

	measure 'Avg Stock per Product' = ```
			
			        DIVIDE([Total Stock], [Total Products in Inventory], 0)
			```
		displayFolder: 08.Inventory metrics
		lineageTag: 8ffb91e4-bc87-478b-a71a-db4e7a306ed3

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Inventory Turnover' = ```
			
			        VAR ProductCOGS = [Total COGS]
			        VAR AvgInventoryValue = [Total Inventory Value]
			        RETURN
			            DIVIDE(ProductCOGS, AvgInventoryValue, 0)
			```
		displayFolder: 08.Inventory metrics
		lineageTag: 7da57ad7-e19e-4bf2-b6fd-b9a410441e63

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Stock Status' =
			
			        VAR CurrentStock = [Stock Qty (Product)]  //  This is UNITS, not $$$
			        RETURN
			            SWITCH(
			                TRUE(),
			               ISBLANK(CurrentStock), "Out of Stock",       //  Handle NULLs first
						   CurrentStock = 0, "Out of Stock",            //  Zero stock
						   CurrentStock < 50, "Critical",               //  Less than 50 units
						   CurrentStock < 100, "Low",                   //  50-99 units
						   CurrentStock < 500, "Normal",                //  100-499 units
							"Overstocked"                               //  500+ units
			            )
		displayFolder: 08.Inventory metrics
		lineageTag: 93075eca-463e-435c-a1bb-b51f4ed8594c

	measure 'Days of Supply' = ```
			
			        VAR CurrentStock = [Stock Qty (Product)]  //  Use quantity, not value
			        VAR DailySales = 
			            CALCULATE(
			                DIVIDE(
			                    SUM(FactSales[OrderQty]), 
			                    DISTINCTCOUNT(FactSales[OrderDate])
			                ),
			                ALL(DimDate)
			            )
			        RETURN
			            SWITCH(
			                TRUE(),
			                ISBLANK(CurrentStock), 0,                    //  NULL stock = 0 days
			                CurrentStock = 0, 0,                          //  Zero stock = 0 days
			                ISBLANK(DailySales) || DailySales = 0, 99999,//  No sales = infinite days
			                DIVIDE(CurrentStock, DailySales, 0)           //  Normal calculation
			        )
			```
		displayFolder: 08.Inventory metrics
		lineageTag: 02c1fef5-12b7-4441-93b0-26e0d716dbe0

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Stock to Sales Ratio' = ```
			
			    VAR CurrentStock = [Stock Qty (Product)]
			    VAR TotalSold = CALCULATE(SUM(FactSales[OrderQty]), ALL(DimDate))
			    RETURN
			        SWITCH(
			            TRUE(),
			            ISBLANK(CurrentStock), BLANK(),              //  NULL stock = blank result
			            ISBLANK(TotalSold) || TotalSold = 0, BLANK(),//  No sales = blank result
			            DIVIDE(CurrentStock, TotalSold, 0)            //  Normal calculation
			        )
			```
		displayFolder: 08.Inventory metrics
		lineageTag: 2354a2fe-b69e-4cf8-b64b-442304c6643e

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Days Inventory on Hand' = ```
			
			    VAR Turnover = [Inventory Turnover]
			    RETURN
			        IF(
			            ISBLANK(Turnover) || Turnover = 0,
			            BLANK(),  // Can't calculate if no turnover
			            DIVIDE(365, Turnover, 0)
			        )
			```
		displayFolder: 08.Inventory metrics
		lineageTag: 54d40dc0-a668-4ed7-8f6a-d5e749752201

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Needs Reorder' = ```
			
			    VAR CurrentStock = [Stock Qty (Product)]
			    VAR DaysOfSupply = [Days of Supply]
			    RETURN
			        SWITCH(
			            TRUE(),
			            ISBLANK(CurrentStock), "Yes",   //  NULL = needs reorder
			            DaysofSupply = 99999, "No",     //  No sales = infinite days
			            CurrentStock = 0, "Yes",        //  Zero stock = needs reorder
			            CurrentStock < 100, "Yes",      //  Low stock = needs reorder
			            "No"                            //  Normal/Overstocked = no reorder
			        )
			```
		displayFolder: 08.Inventory metrics
		lineageTag: 6fb2895c-3116-453f-91c9-401be58d35fa

	measure '% of Total Inventory Value' = ```
			
			        DIVIDE(
			            [Total Inventory Value],
			            CALCULATE([Total Inventory Value], ALL(DimProduct)),
			            0
			        )
			```
		displayFolder: 08.Inventory metrics
		lineageTag: bca9b1cb-e815-4a09-8c17-8c0a5442162a

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'First Purchase Date' = ```
			
			        MIN(FactSales[OrderDate])
			```
		formatString: yyyy-mm-dd
		displayFolder: 05.Customer metrics
		lineageTag: 4ebc75c0-bc92-478e-a720-74820f540885

		annotation PBI_FormatHint = {"isDateTimeCustom":true}

	measure 'Last Purchase Date' = ```
			
			        MAX(FactSales[OrderDate])
			```
		formatString: yyyy-mm-dd
		displayFolder: 05.Customer metrics
		lineageTag: 53d47e87-4de0-49de-8b0a-799e9a684e31

		annotation PBI_FormatHint = {"isDateTimeCustom":true}

	measure 'Days Since Last Purchase' = ```
			
			        DATEDIFF([Last Purchase Date], TODAY(), DAY)
			```
		formatString: 0
		displayFolder: 05.Customer metrics
		lineageTag: 522eb8d9-4a87-4b50-82e6-0f831cf9be06

	measure 'Customer Lifespan Days' = ```
			
			        DATEDIFF([First Purchase Date], [Last Purchase Date], DAY)
			```
		formatString: 0
		displayFolder: 05.Customer metrics
		lineageTag: 118c4a3c-595e-49af-82a0-d0a4217bd6e9

	measure 'Purchase Frequency' = ```
			
			        VAR Lifespan = [Customer Lifespan Days]
			        VAR Orders = [Total Orders]
			        RETURN
			            IF(Lifespan > 0, Orders / (Lifespan / 365.25), BLANK())
			```
		displayFolder: 05.Customer metrics
		lineageTag: 1103f44e-421f-4c92-9e21-9bbb6fb11afe

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure '(Last Purchase Date)' =
			
			CALCULATE(
			    MAX(FactSales[OrderDate]),
			    KEEPFILTERS(FactSales)
			)
		formatString: General Date
		displayFolder: 05.Customer_segment_RFM
		lineageTag: 2e0b2704-dd42-4b55-9986-171c979f8a31

	measure 'Recency (Days)' =
			
			VAR LastPurchase =
			    CALCULATE(MAX(FactSales[OrderDate]), KEEPFILTERS(FactSales))
			VAR MaxDataDate = CALCULATE( MAX(FactSales[OrderDate]), ALL(FactSales) )  -- global max
			RETURN
			IF( ISBLANK(LastPurchase),
			 BLANK(), // <- No purchase, stays blank
			 DATEDIFF( LastPurchase, MaxDataDate, DAY ) )
		formatString: 0
		displayFolder: 05.Customer_segment_RFM
		lineageTag: 8140f790-6dd9-4587-b7a2-62093eb6e688

	measure Frequency =
			
			VAR Orders =
			    CALCULATE(DISTINCTCOUNT(FactSales[SalesOrderID]), KEEPFILTERS(FactSales))
			RETURN IF(Orders = BLANK() || Orders = 0, BLANK(), Orders)
		formatString: 0
		displayFolder: 05.Customer_segment_RFM
		lineageTag: 87d32962-c94e-46b6-8833-6cd1cb5f2b60

	measure Monetary =
			
			VAR SalesAmt =
			    CALCULATE(SUM(FactSales[LineTotal]), KEEPFILTERS(FactSales))
			RETURN IF(SalesAmt = BLANK() || SalesAmt = 0, BLANK(), SalesAmt)
		displayFolder: 05.Customer_segment_RFM
		lineageTag: 904a3029-eb5c-43f8-9f49-8ef589190cff

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'RFM Recency Score' = ```
			
				VAR HasPurchase = NOT ISBLANK([Recency (Days)])
				RETURN
			IF(
			    NOT HasPurchase,
			    BLANK(),
			    // existing logic below
				VAR CustomerRecency = [Recency (Days)]
				VAR MaxR = CALCULATE(
					MAXX( VALUES( DimCustomer[CustomerID] ), [Recency (Days)] ),
					REMOVEFILTERS(DimCustomer)
				)
				VAR MinR = CALCULATE(
					MINX( VALUES( DimCustomer[CustomerID] ), [Recency (Days)] ),
					REMOVEFILTERS(DimCustomer)
				)
				VAR Range = MaxR - MinR
				RETURN
					SWITCH(
						TRUE(),
						Range = 0, BLANK(),
						CustomerRecency <= MinR + Range*0.2, 5,
						CustomerRecency <= MinR + Range*0.4, 4,
						CustomerRecency <= MinR + Range*0.6, 3,
						CustomerRecency <= MinR + Range*0.8, 2,
						1
					)
			)
			```
		formatString: 0
		displayFolder: 05.Customer_segment_RFM
		lineageTag: 5554bcdc-c94b-4863-9e53-0fa8181d3038

	measure 'RFM Frequency Score' = ```
			
				VAR HasPurchase = NOT ISBLANK([Frequency])
				RETURN
			IF(
			    NOT HasPurchase,
			    BLANK(),
			    // existing logic below
				VAR CustomerFreq = [Frequency]
				VAR MaxF = CALCULATE(
						MAXX( VALUES( DimCustomer[CustomerID] ), [Frequency] ),
						REMOVEFILTERS(DimCustomer)
						)
				VAR MinF =  CALCULATE(
					MINX(  VALUES( DimCustomer[CustomerID] ), [Frequency] ),
							REMOVEFILTERS(DimCustomer)
					)
			VAR Range = MaxF - MinF
			RETURN
			    SWITCH(
			        TRUE(),
					Range = 0, BLANK(),
			        CustomerFreq >= MaxF - Range * 0.2, 5,
			        CustomerFreq >= MaxF - Range * 0.4, 4,
			        CustomerFreq >= MaxF - Range * 0.6, 3,
			        CustomerFreq >= MaxF - Range * 0.8, 2,
			        1
			    )
			)
			```
		formatString: 0
		displayFolder: 05.Customer_segment_RFM
		lineageTag: a62828c0-03d5-4442-80c7-84377167cb21

	measure 'RFM Monetary Score' = ```
			
				VAR HasPurchase = NOT ISBLANK([Frequency])
					RETURN
				IF(
					NOT HasPurchase,
					BLANK(),
					// existing logic below
				VAR CustomerMonetary = [Monetary]
				VAR MaxM = CALCULATE(
					MAXX( VALUES( DimCustomer[CustomerID] ), [Monetary] ),
					REMOVEFILTERS(DimCustomer)
					)
				VAR MinM = CALCULATE(
					MINX( VALUES( DimCustomer[CustomerID] ), [Monetary] ),
					REMOVEFILTERS(DimCustomer)
					)
				VAR Range = MaxM - MinM
				RETURN
					SWITCH(
						TRUE(),
						Range = 0, BLANK(),
						CustomerMonetary >= MaxM - Range * 0.2, 5,
						CustomerMonetary >= MaxM - Range * 0.4, 4,
						CustomerMonetary >= MaxM - Range * 0.6, 3,
						CustomerMonetary >= MaxM - Range * 0.8, 2,
						1
					)
			)
			```
		formatString: 0
		displayFolder: 05.Customer_segment_RFM
		lineageTag: 3d4caa8b-866c-4c31-b2f7-0ccf41e1d657

	measure 'RFM Score' =
			
			[RFM Recency Score] * 100 + [RFM Frequency Score] * 10 + [RFM Monetary Score]
		formatString: 0
		displayFolder: 05.Customer_segment_RFM
		lineageTag: 2e1f70a6-bfb7-4753-873c-e02dbe082cb4

	measure 'RFM Segment' = ```
			
				VAR Score = [RFM Score]
				RETURN
				IF(
					ISBLANK(Score),
					"No Purchases",
					SWITCH(
						TRUE(),
						Score >= 555, "Champions",
						Score >= 450, "Loyal Customers",
						Score >= 350, "Potential Loyalists",
						Score >= 250, "Needs Attention",
						Score >= 150, "At Risk",
						"Lost"
					)
				)
			```
		displayFolder: 05.Customer_segment_RFM
		lineageTag: 2bb36c74-e5a8-4a0e-b3a6-57929ba5a377

	measure 'RFM Segment Count' =
			
			VAR SelectedSegment = SELECTEDVALUE(CustomerRFM[RFM Segment])
			RETURN
			CALCULATE(
			    COUNTROWS(CustomerRFM),
			    CustomerRFM[RFM Segment] = SelectedSegment
			)
		formatString: 0
		displayFolder: 05.Customer_segment_RFM
		lineageTag: 3737a1be-99d5-4a62-9408-e4c2e250f5bb

	measure Champions =
			
			CALCULATE(
			    COUNTROWS(CustomerRFM),
			    CustomerRFM[RFM Segment] = "Champions"
			)
		formatString: 0
		displayFolder: 05.Customer_segment_RFM
		lineageTag: 89985710-7c8b-4728-818f-3df32e53fb7b

	measure 'Potential Loyalists' =
			
			CALCULATE(
			    COUNTROWS(CustomerRFM),
			    CustomerRFM[RFM Segment] = "Potential Loyalists"
			)
		formatString: #,0.0
		displayFolder: 05.Customer_segment_RFM
		lineageTag: 92aa71ff-7532-4b36-bd26-10154a68e114

	measure 'Needs Attention' =
			
			CALCULATE(
			    COUNTROWS(CustomerRFM),
			    CustomerRFM[RFM Segment] = "Needs Attention"
			)
		formatString: 0
		displayFolder: 05.Customer_segment_RFM
		lineageTag: 62fe1c9b-c4e4-45b1-ba51-7591d3aef76d

	measure 'At Risk' =
			
			CALCULATE(
			    COUNTROWS(CustomerRFM),
			    CustomerRFM[RFM Segment] = "At Risk"
			)
		formatString: 0
		displayFolder: 05.Customer_segment_RFM
		lineageTag: 702303ed-d38b-4e0f-a5d0-277c7ea8c147

	measure Lost =
			
			CALCULATE(
			    COUNTROWS(CustomerRFM),
			    CustomerRFM[RFM Segment] = "Lost"
			)
		formatString: 0
		displayFolder: 05.Customer_segment_RFM
		lineageTag: 25865100-c7fd-4853-8f5d-56d6dade85dd

	measure 'Loyal Customers' =
			
			CALCULATE(
			    COUNTROWS(CustomerRFM),
			    CustomerRFM[RFM Segment] = "Loyal Customers"
			)
		formatString: #,0.0
		displayFolder: 05.Customer_segment_RFM
		lineageTag: 3e4b53ee-1141-4317-8904-cb88cd69edad

	measure 'Product Rank Bottom' = ```
			
			RANKX(
			    ALL(DimProduct),
			    [Gross Profit],
			    ,
			    ASC,
			    DENSE
			)
			
			```
		formatString: 0
		displayFolder: 06.Product metrics
		lineageTag: e99d9a94-b2c0-41bf-80a4-b18fae7c50d9

	measure 'Product Rank Top' = ```
			
			RANKX(
			    ALL(DimProduct),
			    [Gross Profit],
			    ,
			    DESC,
			    DENSE
			)
			
			```
		formatString: 0
		displayFolder: 06.Product metrics
		lineageTag: d9e64e4b-7652-4574-80c6-5a2278dab621

	measure 'Top Bottom Group' = ```
			
			SWITCH(
			    TRUE(),
			    [Product Rank Top] <= 10, "ðŸ”¼ Top 10 Products",
			    [Product Rank Bottom] <= 10, "ðŸ”½ Bottom 10 Products",
			    BLANK()
			)
			
			```
		displayFolder: 06.Product metrics
		lineageTag: 1e4d7dbd-208a-4f37-86e2-7e045277ba4b

	measure 'Regional Growth %' = ```
			
					VAR PrevRevenue =
						CALCULATE([Total Revenue], SAMEPERIODLASTYEAR(DimDate[Date]))
					RETURN
					DIVIDE(
						[Total Revenue] - PrevRevenue,
						PrevRevenue,
						0
					)
			```
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: 07.Territory metrics
		lineageTag: 538fc6cf-79f0-4c05-9bc9-e2611574238e

	measure 'Vendors Purchase Rank' = ```
			
			    RANKX(
			        ALL(DimVendor[VendorName]),
			        [Total Purchases],
			        ,
			        DESC,
			        Dense
			    )
			```
		formatString: 0
		displayFolder: 09.Purchasing metrics
		lineageTag: 8aae0ec1-c62e-4192-ba42-e4916e54b405

	measure 'Days of Supply (Display)' = ```
			
			        VAR DOS = [Days of Supply]
			        RETURN
			            IF(
			                DOS >= 99999,
			                "âˆž",
			                FORMAT(DOS, "0.0")
			            )
			```
		displayFolder: 08.Inventory metrics
		lineageTag: 9f39613f-fb86-4dd0-ae3e-2ad2f9d1a166

	measure 'Days of Supply Status' = ```
			
			        VAR DOS = [Days of Supply]
			        RETURN
			        SWITCH(
			            TRUE(),
			            ISBLANK(DOS), "No Sales",
			            DOS = 0, "Out of Stock",
			            DOS > 1000, "Infinite",
			            DOS < 7, "Under 1 Week",
			            DOS < 30, "Under 1 Month",
			            DOS < 90, "Under 3 Months",
			            "Healthy"
			        )
			```
		displayFolder: 08.Inventory metrics
		lineageTag: d04b066e-cad7-4413-b860-775c6c4c5b86

	measure 'Total Stock' = ```
			
			        SUMX(
			            ADDCOLUMNS(
			                ALL(DimProduct),
			                "Qty", CALCULATE(SUM(FactInventory[Quantity]))
			            ),
			            COALESCE([Qty], 0)
			        )
			```
		formatString: 0
		displayFolder: 08.Inventory metrics
		lineageTag: 5e4ed53a-7646-45fe-b6de-7894031f6cc2

	measure 'Low Stock Items Count' = ```
			
			        VAR tbl =
			            ADDCOLUMNS(
			                ALL(DimProduct),
			                "Qty", CALCULATE(SUM(FactInventory[Quantity]))
			            )
			        RETURN
			        COUNTROWS(
			            FILTER(tbl, COALESCE([Qty], 0) > 0 && COALESCE([Qty], 0) < 100)
			        )
			```
		formatString: 0
		displayFolder: 08.Inventory metrics
		lineageTag: b8d3c53d-e849-4636-b82e-93348b9b5e0f

	measure 'Out of Stock Items' = ```
			
			        VAR tbl =
			            ADDCOLUMNS(
			                ALL(DimProduct),
			                "Qty", CALCULATE(SUM(FactInventory[Quantity]))
			            )
			        RETURN
			        COUNTROWS(
			            FILTER(tbl, COALESCE([Qty], 0) = 0)
			        )
			```
		formatString: 0
		displayFolder: 08.Inventory metrics
		lineageTag: 7fb8b9b4-b807-4e1a-8601-54493a3c57e2

	measure 'Overstocked Items' = ```
			
			        VAR tbl =
			            ADDCOLUMNS(
			                ALL(DimProduct),
			                "Qty", CALCULATE(SUM(FactInventory[Quantity]))
			            )
			        RETURN
			        COUNTROWS(
			            FILTER(tbl, COALESCE([Qty], 0) >= 500)
			        )
			```
		formatString: 0
		displayFolder: 08.Inventory metrics
		lineageTag: 409bd1bd-9c24-465f-ae13-21902d204b29

	measure 'Stock Qty (Product)' = ```
			
			    VAR Qty = CALCULATE(
			        SUM(FactInventory[Quantity]),
			        KEEPFILTERS(DimProduct[ProductID])
			    )
			    RETURN IF(ISBLANK(Qty), 0, Qty)
			```
		formatString: 0
		displayFolder: 08.Inventory metrics
		lineageTag: 65973650-f7f0-4e75-b1d6-22e55c84f381

	column Value
		lineageTag: 48804b47-f8c2-4fc5-818f-ca109e79ae1b
		summarizeBy: none
		isNameInferred
		sourceColumn: [Value]

		annotation SummarizationSetBy = Automatic

	partition MyMeasures = calculated
		mode: import
		source = ```
				{ ( "Measures Folder" ) }
				
				
				```

	annotation PBI_Id = ac591c24221641bda300a15f9c5b1943

