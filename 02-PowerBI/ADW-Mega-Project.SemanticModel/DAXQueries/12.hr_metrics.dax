-- =============================================
-- Define: HR Metrics
-- =============================================
DEFINE
    // Total employees (current)
    MEASURE MyMeasures[Total Employees] =
        CALCULATE(
            DISTINCTCOUNT(FactEmployeeHistory[BusinessEntityID]),
            FactEmployeeHistory[IsCurrentEmployee] = 1
        )
    // Employees by gender
    MEASURE MyMeasures[Male Employees] =
           CALCULATE(
            IF([Total Employees]== BLANK(),
                0,
                [Total Employees] 
            ),
            DimEmployee[Gender] = "Male"
        )
    MEASURE MyMeasures[Female Employees] =
           CALCULATE(
            IF([Total Employees]== BLANK(),
                0,
                [Total Employees] 
            ),
            DimEmployee[Gender] = "Female"
        )
    // Gender ratio
    MEASURE MyMeasures[Gender Ratio] =
        DIVIDE([Male Employees], [Female Employees], 0)
    // Average tenure (years)
    MEASURE MyMeasures[Avg Tenure Years] =
        VAR MaxDate = MAX(DimDate[Date])
        VAR TenureDays =
            CALCULATE(
                AVERAGEX(
                    FILTER(
                        FactEmployeeHistory,
                        FactEmployeeHistory[IsCurrentEmployee] = 1
                    ),
                    DATEDIFF(FactEmployeeHistory[StartDate], MaxDate, DAY)
                )
            )
        RETURN
            TenureDays / 365.25

    // // Average employee age
    MEASURE MyMeasures[Avg Employee Age] =
        VAR MaxDate = MAX(DimDate[Date])
        RETURN
        AVERAGEX(
            SELECTCOLUMNS(
                VALUES(FactEmployeeHistory[BusinessEntityID]), 
                "BirthDate", LOOKUPVALUE(
                                DimEmployee[BirthDate],
                                DimEmployee[BusinessEntityID], 
                                FactEmployeeHistory[BusinessEntityID]
                            )
            ),
            DATEDIFF([BirthDate], MaxDate, DAY) / 365.25
)


        
    // Turnover rate (past year)
    MEASURE MyMeasures[Turnover Rate %] =
    VAR CurrentDate =
        MAX(DimDate[Date])  -- âœ… scalar date for context
    VAR OneYearAgo =
        DATE(YEAR(CurrentDate) - 1, MONTH(CurrentDate), DAY(CurrentDate))
    VAR EmployeesLeft =
        CALCULATE(
            DISTINCTCOUNT(FactEmployeeHistory[BusinessEntityID]),
            FactEmployeeHistory[EndDate] <> BLANK(),
            FactEmployeeHistory[EndDate] >= OneYearAgo
        )
    VAR AvgHeadcount = [Total Employees]
    RETURN
        DIVIDE(EmployeesLeft, AvgHeadcount, 0)

-- =============================================
-- Evaluate: HR Performance by Department
-- =============================================
EVALUATE
SUMMARIZECOLUMNS(
    DimDepartment[DepartmentName],
    "Total Employees", [Total Employees],
    "Male Employees", [Male Employees],
    "Female Employees", [Female Employees],
    "Gender Ratio", [Gender Ratio],
    "Avg Tenure Years", [Avg Tenure Years],
    "Avg Employee Age", [Avg Employee Age],
    "Turnover Rate %", [Turnover Rate %]
)
ORDER BY [Total Employees] DESC
